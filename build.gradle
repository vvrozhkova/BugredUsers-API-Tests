plugins {
    id 'java'
    id 'io.qameta.allure' version '2.8.1'
    id "io.freefair.lombok" version "6.4.2"
}

group 'io.vvrozhkova'
version '1.0'

repositories {
    mavenCentral()
}

def allureVersion = "2.18.1",
    junitVersion = "5.8.2"

allure {
    version = allureVersion
    autoconfigure = true
    aspectjweaver = true
    configuration = "testImplementation"
    useJUnit5 {
        version = allureVersion
    }
}

dependencies {
    testImplementation(
            "org.junit.jupiter:junit-jupiter:$junitVersion",
            "io.rest-assured:rest-assured:5.1.1",
            "org.aspectj:aspectjweaver:1.9.9.1",
            "io.qameta.allure:allure-rest-assured:$allureVersion",
            "io.qameta.allure:allure-assertj:$allureVersion",
            "org.aeonbits.owner:owner:1.0.12",
            "org.projectlombok:lombok:1.18.24",
            'org.assertj:assertj-core:3.23.1',
            "com.github.javafaker:javafaker:1.0.2",
            "com.fasterxml.jackson.core:jackson-databind:2.13.3"
    )
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

tasks.withType(Test) {
    systemProperties(System.getProperties())
    useJUnitPlatform()
    if (System.getProperty("threads") != null) {
        systemProperties += [
                'junit.jupiter.execution.parallel.enabled'                 : true,
                'junit.jupiter.execution.parallel.mode.default'            : 'concurrent',
                'junit.jupiter.execution.parallel.mode.classes.default'    : 'concurrent',
                'junit.jupiter.execution.parallel.config.strategy'         : 'fixed',
                'junit.jupiter.execution.parallel.config.fixed.parallelism': System.getProperty("threads").toInteger()
        ]
    }

    testLogging {
        lifecycle {
//            events "started", "failed"
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}